area(alfa: Real) = (x, y: Inteiro) =>
  se alfa * (x - 1) >= y ou alfa * x <= y então
    abs(alfa * (x - 0.5) - y)
  senão    # a reta cruza a linha do caminho 
    a = (y / alfa - x) / 2
    a * (y - alfa * x) + (a + 0.5) * (y - alfa * (x - 1))
  fim

quantidade(s: Texto, c: Caractere) = s.selecione(_ == c).tamanho
inclinacao(s: Texto) = quantidade(s, 'U') / quantidade(s, 'R')

var entrada, t := leia_texto, 1
enquanto entrada <> "S" faça
  d = area(inclinacao(entrada))
  var area_total, x, y := 0.0, 0, 0
  para c em entrada faça
    se c == 'R' então
      x := x + 1
      area_total := area_total + d(x, y)
    senão
      y := y + 1
    fim
  fim
  escreva "{t}. {area_total formato "%.3f"}"
  entrada, t := leia_texto, t + 1
fim
